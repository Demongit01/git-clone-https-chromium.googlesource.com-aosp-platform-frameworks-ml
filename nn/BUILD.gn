# Copyright 2020 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":common_random"
  ]
  if (use.test) {
    deps += [ ":chromeos_testrunner" ]
  }
}

pkg_config("target_defaults") {
  cflags = [
    "-Wall",
    "-Werror",
    "-Wextra",
  ]
  cflags_cc = [ "-std=c++17" ]
  include_dirs = [
    "${sysroot}/usr/include/aosp",
  ]
  defines = []
  pkg_deps = [ "libnnapi-support" ]
}

pkg_config("common_defaults") {
  configs = [
    ":target_defaults",
  ]
  include_dirs = [
    "chromeos/includes",
    "common/include",
    "${sysroot}/usr/include/eigen3",
    "${sysroot}/usr/include/tensorflow",
    "common/random",
  ]
  defines = []
  pkg_deps = [ "libnnapi-support" ]
}

static_library("common_random") {
  configs += [
    ":common_defaults",
  ]
  sources = [
    "common/random/guarded_philox_random.cc",
    "common/random/random.cc",
    "common/random/simple_philox.cc",
  ]
}

if (use.test) {
  executable("chromeos_testrunner") {
    include_dirs = [
      "common/random",
    ]
    configs += [
      ":common_defaults",
      "//common-mk:test",
    ]
    deps = [
      ":common_random",
      "//common-mk/testrunner:testrunner",
    ]
    sources = [
      "chromeos/tests/common/includes_test.cc",
      "chromeos/tests/common/random_test.cc"
    ]
  }
}
